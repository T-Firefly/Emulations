apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.firefly.emulationstation"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 7
        versionName "1.2.4"

        // For room testing
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "VERSION_CHECK_URL",
                    "\"http://download.t-firefly.com/product/station/App/GameStation/update.json\""
        }
        debug {
            buildConfigField "String", "VERSION_CHECK_URL",
                    "\"http://192.168.15.175/GameStation/update.json\""
        }
    }

    // For room testing
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

configurations {
    compile.exclude group: 'stax', module: 'stax-api'
    compile.exclude group: 'xpp3'
}

repositories {
    flatDir {
        dirs 'libs'
    }
}


afterEvaluate { project ->
    project.tasks.each { task ->
        if (task.name == "assembleRelease") {
            task.doLast{
                executePostReleaseAssemble()
            }
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${variant.name}-v${variant.versionName}.apk"
    }
}

def executePostReleaseAssemble() {
    file("update.json").text = """\
{"version": "v${android.defaultConfig.versionName}",\
"versionCode": ${android.defaultConfig.versionCode},\
"url": "release-v${android.defaultConfig.versionName}.apk",\
"logs": {
    "default": "update log here.",
    "zh": "更新日志" \
  } \
}
"""
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:leanback-v17:27.1.1'
    compile 'com.android.support:preference-leanback-v17:27.1.1'
    compile 'com.android.support:appcompat-v7:27.1.1'
    compile 'com.android.support:palette-v7:27.1.1'
    implementation("com.github.bumptech.glide:glide:4.7.1")
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    compile 'com.github.bumptech.glide:okhttp3-integration:4.7.1'
    compile 'com.android.support:support-v4:27.1.1'
    compile 'com.google.dagger:dagger:2.15'
    compile 'com.google.dagger:dagger-android:2.15'
    compile 'com.google.dagger:dagger-android-support:2.12' // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.12'
    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-simplexml:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation "android.arch.persistence.room:rxjava2:1.1.1"
    androidTestCompile "android.arch.persistence.room:testing:1.1.1"

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:1.0.2'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:1.0.2'
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.2'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation 'com.google.code.gson:gson:2.8.2'
}
